{
  "cells": [
    {
      "cell_type": "markdown",
      "id": "d3823d72-e2d1-44c8-836c-f1f3e181553c",
      "metadata": {
        "id": "d3823d72-e2d1-44c8-836c-f1f3e181553c"
      },
      "source": [
        "# Introduction to JACC.jl\n",
        "<img src=\"https://github.com/PhilipFackler/JACC-applications/blob/main/tutorial/images/JACC-logo.png?raw=1\" alt=\"JACC logo\" style=\"width:15%;height:auto;\">"
      ]
    },
    {
      "cell_type": "markdown",
      "source": [
        "## Motivation\n",
        "Different HPC systems have different hardware and thus different vendor-provided APIs.\n",
        "\n",
        "Different users have different levels/types of expertise.\n",
        "\n",
        "Therefore we want:\n",
        "- performance portability\n",
        "- programming productivity\n",
        "  **_Program once; deploy everywhere!_**\n",
        "- performance \"practicability\"?\n",
        "\n",
        "<img src=\"https://github.com/PhilipFackler/JACC-applications/blob/main/tutorial/images/HPC-systems-vendors.png?raw=1\" alt=\"HPC Systems and Vendors\" style=\"width:75%;height:auto;\">"
      ],
      "metadata": {
        "id": "JOe1iGd3ubz8"
      },
      "id": "JOe1iGd3ubz8"
    },
    {
      "cell_type": "markdown",
      "source": [
        "### Options for (data) parallelism in Julia\n",
        "CPU multi-threading\n",
        "- Threads (built-in)\n",
        "- Polyester\n",
        "- OhMyThreads\n",
        "\n",
        "GPU kernel model (fine-granularity):\n",
        "- Vendor-specific packages:\n",
        "  - CUDA, AMDGPU, oneAPI, Metal\n",
        "- [KernelAbstractions](https://github.com/JuliaGPU/KernelAbstractions.jl)\n",
        "  - Provides (mostly) portable interface implemented by each vendor package"
      ],
      "metadata": {
        "id": "ufcyfKSStwXS"
      },
      "id": "ufcyfKSStwXS"
    },
    {
      "cell_type": "markdown",
      "source": [
        "### The Aim of JACC\n",
        "\n",
        "Provide a familiar interface and smart defaults, bringing good performance closer to domain scientists who may not be experts in computer science.\n",
        "\n",
        "- high-level unified Julia front-end on top of multiple backends (Threads, CUDA, AMDGPU, oneAPI)\n",
        "- Hide low-level and device-specific implementation\n",
        "- Improve programming productivity for science and HPC\n",
        "- A growing community\n",
        "  - LBNL/NERSC, LANL, Argonne, MIT, ETHZ, BSC, Cerfacs, FI/CCQ, …\n",
        "  - You are welcome to join (JACC community meetings once a month)\n",
        "  - Reach out on [github](https://github.com/JuliaORNL/JACC.jl/discussions)\n",
        "<img src=\"https://github.com/PhilipFackler/JACC-applications/blob/main/tutorial/images/JACC-stack.png?raw=1\" alt=\"JACC stack\" style=\"width:25%;height:auto;\">"
      ],
      "metadata": {
        "id": "kfXgQjXsuodj"
      },
      "id": "kfXgQjXsuodj"
    },
    {
      "cell_type": "markdown",
      "id": "28012c00-99bd-47dd-bd62-6735ea0c2e47",
      "metadata": {
        "id": "28012c00-99bd-47dd-bd62-6735ea0c2e47"
      },
      "source": [
        "## Simple Exercises\n",
        "1. Allocate an array and use a parallel_for to initialize the elements (e.g., using the index values)\n",
        "2. Allocate an array of ones and find the sum of the elements using parallel_reduce.\n",
        "3. Try #2 using a parallel_for with @atomic\n",
        "\n",
        "(Bonus) Create a struct holding an array and use it in a kernel. (This is not a challenge for CPU-only code)"
      ]
    },
    {
      "cell_type": "markdown",
      "id": "b5609336-4dd7-4fc2-a762-4929dca86389",
      "metadata": {
        "id": "b5609336-4dd7-4fc2-a762-4929dca86389"
      },
      "source": [
        "## Getting started with JACC"
      ]
    },
    {
      "cell_type": "markdown",
      "id": "e514ec9c-7687-454e-8a15-4e01d6024e5b",
      "metadata": {
        "id": "e514ec9c-7687-454e-8a15-4e01d6024e5b"
      },
      "source": [
        "##### 1. Add JACC as a dependency"
      ]
    },
    {
      "cell_type": "code",
      "execution_count": null,
      "id": "4c537790-43cb-459b-aa83-87b318325e38",
      "metadata": {
        "id": "4c537790-43cb-459b-aa83-87b318325e38"
      },
      "outputs": [],
      "source": [
        "import Pkg\n",
        "Pkg.add(\"JACC\")"
      ]
    },
    {
      "cell_type": "markdown",
      "id": "aa692897-b710-4e5c-be5f-fdc9f0e94eea",
      "metadata": {
        "id": "aa692897-b710-4e5c-be5f-fdc9f0e94eea"
      },
      "source": [
        "##### 2. Set backend\n",
        "\n",
        "The default backend is Threads"
      ]
    },
    {
      "cell_type": "code",
      "execution_count": null,
      "id": "c3ea286e-0b9e-4657-8d44-ba8eb77f4dbc",
      "metadata": {
        "id": "c3ea286e-0b9e-4657-8d44-ba8eb77f4dbc"
      },
      "outputs": [],
      "source": [
        "import JACC\n",
        "@show JACC.backend\n",
        "@show JACC.supported_backends\n",
        ";"
      ]
    },
    {
      "cell_type": "markdown",
      "source": [
        "Calling `set_backend` will install the appropriate backend package as a dependency for the current project (don't commit this!).\n",
        "\n",
        "NOTE: in some cases you may need to configure the backend package before proceeding to use it."
      ],
      "metadata": {
        "id": "xOA4VrgXw3_t"
      },
      "id": "xOA4VrgXw3_t"
    },
    {
      "cell_type": "code",
      "execution_count": null,
      "id": "71010202-27a0-4d8f-804c-7b0c4d89558c",
      "metadata": {
        "id": "71010202-27a0-4d8f-804c-7b0c4d89558c"
      },
      "outputs": [],
      "source": [
        "import JACC\n",
        "JACC.set_backend(\"oneapi\")"
      ]
    },
    {
      "cell_type": "markdown",
      "id": "b2d306bb-1ee2-4861-a1d7-a7eefae58c1b",
      "metadata": {
        "id": "b2d306bb-1ee2-4861-a1d7-a7eefae58c1b"
      },
      "source": [
        "##### 3. Restart Julia\n",
        "For this notebook, use the menu: \"Kernel\" > \"Restart Kernel...\" (With the REPL in a terminal you would simply quit and restart `julia`.)"
      ]
    },
    {
      "cell_type": "markdown",
      "id": "c1d1a1ad-62f2-45b1-9c0d-101156d17f88",
      "metadata": {
        "id": "c1d1a1ad-62f2-45b1-9c0d-101156d17f88"
      },
      "source": [
        "##### 4. Load extension before use\n",
        "\n",
        "```julia\n",
        "JACC.@init_backend\n",
        "```\n",
        "\n",
        "Equivalent to\n",
        "```julia\n",
        "import <backend-package>\n",
        "```"
      ]
    },
    {
      "cell_type": "code",
      "execution_count": null,
      "id": "8d6114ed-3757-49e4-87fa-cac2e9eda110",
      "metadata": {
        "id": "8d6114ed-3757-49e4-87fa-cac2e9eda110"
      },
      "outputs": [],
      "source": [
        "import JACC\n",
        "JACC.@init_backend"
      ]
    },
    {
      "cell_type": "markdown",
      "id": "82af2ec1-5086-4896-87df-dd481a8ca886",
      "metadata": {
        "id": "82af2ec1-5086-4896-87df-dd481a8ca886"
      },
      "source": [
        "## JACC paradigm basics"
      ]
    },
    {
      "cell_type": "markdown",
      "source": [
        "### Arrays\n",
        "There is no separate array type in JACC. Array functions return the backend array type directly.\n",
        "\n",
        "`JACC.array_type()` will give the corresponding `UnionAll` (non-instantuiated) parametric type.\n",
        "- useful for function parameters and struct members."
      ],
      "metadata": {
        "id": "Vxrm2lWeocLO"
      },
      "id": "Vxrm2lWeocLO"
    },
    {
      "cell_type": "code",
      "source": [
        "@show JACC.array_type()\n",
        "@show JACC.array_type(){Float64, 2}\n",
        ";"
      ],
      "metadata": {
        "id": "mPV3-oyhoS-_"
      },
      "id": "mPV3-oyhoS-_",
      "execution_count": null,
      "outputs": []
    },
    {
      "cell_type": "markdown",
      "id": "43bc4bd1-2262-49c8-8155-cf8de1d08519",
      "metadata": {
        "id": "43bc4bd1-2262-49c8-8155-cf8de1d08519"
      },
      "source": [
        "#### Create arrays on-device\n",
        "\n",
        "- `JACC.ones`\n",
        "- `JACC.zeros`\n",
        "- `JACC.fill`\n",
        "- `JACC.array`\n",
        "\n",
        "Without a type parameter, these use the default float type (usually `Float64`). You can check with `JACC.default_float()`.\n",
        "\n",
        "### Transfer\n",
        "\n",
        "- `JACC.to_device` or `JACC.array(:: AbstractArray)`\n",
        "- `JACC.to_host`\n",
        "\n",
        "For `Threads` backend, this returns the argument."
      ]
    },
    {
      "cell_type": "code",
      "execution_count": null,
      "id": "a4f60011-dcc2-4c09-8358-3a818b4acbfb",
      "metadata": {
        "id": "a4f60011-dcc2-4c09-8358-3a818b4acbfb"
      },
      "outputs": [],
      "source": [
        "@show JACC.default_float()\n",
        "\n",
        "a = randn(3,2)\n",
        "@show a, typeof(a)\n",
        "\n",
        "ad = JACC.to_device(a)\n",
        "@show ad, typeof(ad)\n",
        "\n",
        "ad1 = JACC.array(3, 2) # uninitialized\n",
        "@show ad1, typeof(ad1)\n",
        "\n",
        "ad2 = JACC.ones(2, 2)\n",
        "@show ad2, typeof(ad2)\n",
        "\n",
        "ad3 = JACC.fill(2.5, 5)\n",
        "@show ad3, typeof(ad3)\n",
        "\n",
        "ah3 = JACC.to_host(ad3)\n",
        "@show ah3, typeof(ah3)\n",
        ";"
      ]
    },
    {
      "cell_type": "markdown",
      "id": "6a93b367-41c6-4ae1-bd14-6099839ac96b",
      "metadata": {
        "id": "6a93b367-41c6-4ae1-bd14-6099839ac96b"
      },
      "source": [
        "### Parallel Constructs\n",
        "\n",
        "Primary parameters:\n",
        "- kernel domain- kernel function\n",
        "- parameters for kernel function\n",
        "\n",
        "*NOTE:* kernel function must take thread index(es) as fist parameter(s).\n",
        "\n",
        "#### Function style\n",
        "- `JACC.parallel_for(N, f, x)`\n",
        "- `JACC.parallel_reduce(N, f, x)`\n",
        "\n",
        "#### Macro style\n",
        "- `JACC.@parallel_for range=N f(x)`\n",
        "- `JACC.@parallel_reduce range=N f(x)`\n",
        "\n",
        "*NOTE:* keyword arguments in macro calls cannot use spaces\n",
        "\n",
        "Example: define `axpy` and `dot` as kernel functions for 1D and 2D arrays:"
      ]
    },
    {
      "cell_type": "code",
      "execution_count": null,
      "id": "3e0da3ae-29aa-4435-aec2-6226638483ae",
      "metadata": {
        "id": "3e0da3ae-29aa-4435-aec2-6226638483ae"
      },
      "outputs": [],
      "source": [
        "# 1D\n",
        "\n",
        "## define kernel functions\n",
        "function axpy(i, alpha, x, y)\n",
        "    x[i] += alpha * y[i]\n",
        "end\n",
        "\n",
        "function dot(i, x, y)\n",
        "    return x[i] * y[i]\n",
        "end\n",
        "\n",
        "## construct arrays\n",
        "SIZE = 1_000\n",
        "x = round.(rand(JACC.default_float(), SIZE) * 100)\n",
        "y = round.(rand(JACC.default_float(), SIZE) * 100)\n",
        "alpha = 2.5\n",
        "dx = JACC.to_device(x)\n",
        "dy = JACC.to_device(y)\n",
        "\n",
        "## do parallel operations\n",
        "JACC.parallel_for(SIZE, axpy, alpha, dx, dy)\n",
        "res = JACC.parallel_reduce(SIZE, dot, dx, dy)\n",
        "@show res\n",
        "\n",
        "# 2D\n",
        "\n",
        "## define kernel functions\n",
        "function axpy(i, j, alpha, x, y)\n",
        "    x[i, j] += alpha * y[i, j]\n",
        "end\n",
        "\n",
        "function dot(i, j, x, y)\n",
        "    return x[i, j] * y[i, j]\n",
        "end\n",
        "\n",
        "## construct arrays\n",
        "SIZE = 1_000\n",
        "x = round.(rand(Float64, SIZE, SIZE) * 100)\n",
        "y = round.(rand(Float64, SIZE, SIZE) * 100)\n",
        "alpha = 2.5\n",
        "dx = JACC.to_device(x)\n",
        "dy = JACC.to_device(y)\n",
        "\n",
        "## do parallel operations\n",
        "JACC.@parallel_for range=(SIZE, SIZE) axpy(alpha, dx, dy)\n",
        "res = JACC.@parallel_reduce range=(SIZE, SIZE) dot(dx, dy)\n",
        "@show JACC.to_host(res)[]\n",
        ";"
      ]
    },
    {
      "cell_type": "markdown",
      "id": "c2d99b6a-4326-4686-9084-0c7213da91f7",
      "metadata": {
        "id": "c2d99b6a-4326-4686-9084-0c7213da91f7"
      },
      "source": [
        "#### Performance results for simple kernels\n",
        "<img src=\"https://github.com/PhilipFackler/JACC-applications/blob/main/tutorial/images/JACC-perfresults-01.png?raw=1\" alt=\"Performance Results\" style=\"width:90%;height:auto;\">"
      ]
    },
    {
      "cell_type": "markdown",
      "id": "bebd6688-2840-4617-8e0c-d7c6bc54bfde",
      "metadata": {
        "id": "bebd6688-2840-4617-8e0c-d7c6bc54bfde"
      },
      "source": [
        "## Julia `do` syntactic sugar"
      ]
    },
    {
      "cell_type": "code",
      "execution_count": null,
      "id": "1d1c4185-b22c-4b4b-8b4e-be7f941da644",
      "metadata": {
        "id": "1d1c4185-b22c-4b4b-8b4e-be7f941da644"
      },
      "outputs": [],
      "source": [
        "N = 100\n",
        "alpha = 2.5\n",
        "x = ones(N)\n",
        "y = ones(N)\n",
        "x_d = JACC.ones(N)\n",
        "y_d = JACC.ones(N)\n",
        "\n",
        "### Serial loop\n",
        "for i = 1:N\n",
        "    @inbounds x[i] += alpha * y[i]\n",
        "end\n",
        "###\n",
        "\n",
        "### JACC do-style\n",
        "JACC.parallel_for(N, alpha, x_d, y_d) do i, a, x, y\n",
        "    @inbounds x[i] += a * y[i]\n",
        "end\n",
        "###\n",
        ";"
      ]
    },
    {
      "cell_type": "markdown",
      "id": "745cf0bf-216b-49b1-b9f0-3f91c18ce1a5",
      "metadata": {
        "id": "745cf0bf-216b-49b1-b9f0-3f91c18ce1a5"
      },
      "source": [
        "_**NOTE**_: Items must be passed explicitly. If you implicitly reference names from the parent surrounding scope, you will be attempting to access host memory from the device (unless you're using the \"threads\" backend)."
      ]
    },
    {
      "cell_type": "markdown",
      "id": "92cffa70-fe54-4745-811e-1abe82b8dca6",
      "metadata": {
        "id": "92cffa70-fe54-4745-811e-1abe82b8dca6"
      },
      "source": [
        "Let's see a slightly more involved example and use a named tuple for arguments.\n",
        "\n",
        "```julia\n",
        "### Serial loop\n",
        "for i = 1:length(events)\n",
        "    @inbounds begin\n",
        "        event = events[i, 6:8]\n",
        "        weight = events[i, 1]\n",
        "        for op in transforms\n",
        "            v = op * event\n",
        "            atomic_push!(hist, v, weight)\n",
        "        end\n",
        "    end\n",
        "end\n",
        "\n",
        "### JACC do-style\n",
        "JACC.parallel_for(   # named tuple\n",
        "    length(events), (h = hist, events, transforms)) do i, t\n",
        "    @inbounds begin\n",
        "        event = t.events[i, 6:8]\n",
        "        weight = t.events[i, 1]\n",
        "        for op in t.transforms\n",
        "            v = op * event\n",
        "            atomic_push!(t.h, v, weight)\n",
        "        end\n",
        "    end\n",
        "end\n",
        "###\n",
        "\n",
        "### JACC with anonymous function\n",
        "JACC.parallel_for(length(events),\n",
        "    (i, t) -> begin\n",
        "        @inbounds begin\n",
        "            event = t.events[i, 6:8]\n",
        "            weight = t.events[i, 1]\n",
        "            for op in t.transforms\n",
        "                v = op * event\n",
        "                atomic_push!(t.h, v, weight)\n",
        "            end\n",
        "        end\n",
        "    end,\n",
        "    (h = hist, events, transforms), # named tuple\n",
        ")\n",
        "###\n",
        "```"
      ]
    },
    {
      "cell_type": "markdown",
      "id": "47861995-0d9e-4966-8555-4c22b67dd3b5",
      "metadata": {
        "id": "47861995-0d9e-4966-8555-4c22b67dd3b5"
      },
      "source": [
        "The basic `JACC.parallel_reduce` API:\n",
        "\n",
        "```julia\n",
        "parallel_reduce(N, f, x…; op, init) -> typeof(init)\n",
        "parallel_reduce(N, f, x…) = parallel_reduce(N, f, x…; op = +, init = 0.0)\n",
        "parallel_reduce([op = +,] a::AbstractArray; init = default_init(eltype(a), op)) -> typeof(init)\n",
        "# op ∊ {+, *, min, max, <user-defined>}\n",
        "```\n",
        "\n",
        "Here are a few simple examples:"
      ]
    },
    {
      "cell_type": "code",
      "execution_count": null,
      "id": "d66f752b-d55a-45a5-99cd-13fcdbb8be9a",
      "metadata": {
        "id": "d66f752b-d55a-45a5-99cd-13fcdbb8be9a"
      },
      "outputs": [],
      "source": [
        "a = JACC.array([i for i in 1:10])\n",
        "@show JACC.parallel_reduce(a)\n",
        "@show JACC.parallel_reduce(min, a)\n",
        "\n",
        "b = JACC.fill(2, 10)\n",
        "b_min = JACC.parallel_reduce(10, (i, b) -> b[i] + i, b; op = min, init = Inf)\n",
        "@show b_min\n",
        "b_max = JACC.@parallel_reduce range=10 op=max init=-Inf JACC.elem_access(b)\n",
        "@show JACC.to_host(b_max)[]\n",
        "\n",
        "dp = JACC.parallel_reduce(10, a, b) do i, a, b\n",
        "    a[i] * b[i]\n",
        "end\n",
        "@show dp\n",
        ";"
      ]
    },
    {
      "cell_type": "markdown",
      "id": "f3346673-4c85-4475-8059-f5d1dac2ee4d",
      "metadata": {
        "id": "f3346673-4c85-4475-8059-f5d1dac2ee4d"
      },
      "source": [
        "## Notes for GPU programming\n",
        "- Any function can be used in a kernel (without special annotation) as long as it doesn't allocate\n",
        "- JACC imports the `@atomic` macro from [Atomix.jl](https://github.com/JuliaConcurrent/Atomix.jl)\n",
        "- Use [StaticArrays.jl](https://github.com/JuliaArrays/StaticArrays.jl) for small fixed-size arrays\n",
        "- Use [Adapt.jl](https://github.com/JuliaGPU/Adapt.jl) if you need to put GPU arrays in a struct"
      ]
    },
    {
      "cell_type": "code",
      "execution_count": null,
      "id": "96d84439-98d0-478d-aacd-cdd6a14d5198",
      "metadata": {
        "id": "96d84439-98d0-478d-aacd-cdd6a14d5198"
      },
      "outputs": [],
      "source": []
    }
  ],
  "metadata": {
    "kernelspec": {
      "display_name": "Julia",
      "name": "julia"
    },
    "language_info": {
      "name": "julia"
    },
    "colab": {
      "provenance": []
    }
  },
  "nbformat": 4,
  "nbformat_minor": 5
}